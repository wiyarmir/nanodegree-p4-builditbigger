apply plugin: 'com.android.application'
apply plugin: 'launcher-label'


def BANNER_AD_UNIT = '"' + JokesBannerAdUnitProp + '"' ?: '"Define banner Ad unit ID"';
def INTER_AD_UNIT = '"' + JokesInterAdUnitProp + '"' ?: '"Define intersitial Ad unit ID"';

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "es.guillermoorellana.builditbigger.app"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        all { type ->
            type.buildConfigField 'String', 'BANNER_AD_UNIT', BANNER_AD_UNIT
            type.buildConfigField 'String', 'INTER_AD_UNIT', INTER_AD_UNIT
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            debuggable false
        }
    }
    productFlavors {
        free {
            applicationId 'es.guillermoorellana.builditbigger.app.free'
            ext.labels = ["FREE"]
        }

        pro {
            applicationId 'es.guillermoorellana.builditbigger.app.pro'
            ext.labels = ["PRO"]
        }
    }
    packagingOptions {
        exclude 'LICENSE.txt'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    // Added for AdMob.
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile(project(path: ':backend', configuration: 'android-endpoints')) {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
    }
    compile project(path: ':jokedroid')
    freeCompile 'com.google.android.gms:play-services-ads:7.5.0'
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test:rules:0.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2'
    androidTestCompile 'org.assertj:assertj-core:1.0.0'
    androidTestCompile 'com.squareup.assertj:assertj-android:1.0.0'
}
